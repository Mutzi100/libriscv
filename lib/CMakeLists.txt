#
# C++17 RISC-V emulator library
#

option(RISCV_DEBUG  "Enable debugging features in the RISC-V machine" OFF)
option(RISCV_ICACHE "Enable instruction decoder cache" ON)
option(RISCV_EXT_A  "Enable RISC-V atomic instructions" ON)
option(RISCV_EXT_C  "Enable RISC-V compressed instructions" ON)
option(RISCV_EXT_F  "Enable RISC-V floating-point instructions" ON)
option(RISCV_MULTIPROCESS  "Enable multiprocessing feature" ON)
option(RISCV_EXPERIMENTAL  "Enable experimental features" OFF)
option(RISCV_SHARED_PT "Enable shared page tables (performance penalty)" OFF)
option(RISCV_USE_RH_HASH "Enable robin-hood hashing for page tables" OFF)

if (RISCV_USE_RH_HASH)
	include(FetchContent)
	FetchContent_Declare(
		robinhood
		GIT_REPOSITORY https://github.com/martinus/robin-hood-hashing.git
		CONFIGURE_COMMAND ""
		BUILD_COMMAND ""
		GIT_TAG master
		GIT_SHALLOW TRUE
		GIT_PROGRESS TRUE)

	FetchContent_GetProperties(robinhood)
	if (NOT robinhood_POPULATED)
		FetchContent_Populate(robinhood)
	endif()

	add_library(robinhood INTERFACE)
	target_include_directories(robinhood INTERFACE ${robinhood_SOURCE_DIR}/src/include)
	message(STATUS "robinhood sources: ${robinhood_SOURCE_DIR}")
endif()

set (SOURCES
		libriscv/cpu.cpp
		libriscv/decoder_cache.cpp
		libriscv/machine.cpp
		libriscv/memory.cpp
		libriscv/memory_rw.cpp
		libriscv/native_libc.cpp
		libriscv/native_threads.cpp
		libriscv/posix_threads.cpp
		libriscv/socket_calls.cpp
		libriscv/system_calls.cpp
		libriscv/rv32i.cpp
		libriscv/rv64i.cpp
		libriscv/rv128i.cpp
		libriscv/serialize.cpp
		libriscv/util/crc32c.cpp
	)
if (RISCV_DEBUG)
	list(APPEND SOURCES
		libriscv/debug.cpp
	)
endif()
if (RISCV_MULTIPROCESS)
	list(APPEND SOURCES
		libriscv/cpu_multiprocess.cpp
	)
endif()

add_library(riscv ${SOURCES})
set_target_properties(riscv PROPERTIES CXX_STANDARD 17)
target_include_directories(riscv PUBLIC .)
target_compile_options(riscv PRIVATE -Wall -Wextra)
if (RISCV_USE_RH_HASH)
	target_link_libraries(riscv PUBLIC robinhood)
	target_compile_definitions(riscv PUBLIC RISCV_USE_RH_HASH=1)
endif()
if (RISCV_DEBUG)
	target_compile_definitions(riscv PUBLIC RISCV_DEBUG=1)
endif()
if (RISCV_EXT_A)
	target_compile_definitions(riscv PUBLIC RISCV_EXT_ATOMICS=1)
endif()
if (RISCV_EXT_C)
	target_compile_definitions(riscv PUBLIC RISCV_EXT_COMPRESSED=1)
endif()
if (RISCV_EXT_F)
	target_compile_definitions(riscv PUBLIC RISCV_EXT_FLOATS=1)
endif()
if (RISCV_ICACHE)
	target_compile_definitions(riscv PUBLIC RISCV_INSTR_CACHE=1)
endif()
if (RISCV_MULTIPROCESS)
	target_compile_definitions(riscv PUBLIC RISCV_MULTIPROCESS=1)
endif()
if (RISCV_EXPERIMENTAL)
	target_compile_definitions(riscv PUBLIC RISCV_INSTR_CACHE_PREGEN=1)
endif()
if (RISCV_SHARED_PT)
	target_compile_definitions(riscv PUBLIC RISCV_SHARED_PAGETABLES=1)
endif()
